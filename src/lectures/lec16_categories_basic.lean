/- Copyright (c) 2022 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
----------------
# Basics of Categories
Sina Hazratpour
Introduction to Proof  
MATH 301, Johns Hopkins University, Fall 2022   
-/

/-
-- "_Category theory takes a birdâ€™s eye view of mathematics. From high in the sky, details become invisible, but we can spot patterns that were impossible to de- tect from ground level._" 

-- From "Basic Category Theory" by Tom Leinster
-- -/



-- import tactic.basic
import ..prooflab
import lectures.lec15_integers
import tactic.basic

open PROOFS
open PROOFS.STR


/-
We already have so far seen many interesting __objects__ in this course: 

- Types (introduced in lecture 1)
- Pointed Types (introduced in lecture 11)
- Propositions (introduced in lecture 1 and 6) and Predicates/Relations (introduced in lecture 7)
- Graphs (introduced in hackathon)
- Quasigroups (introduced HW 8)
- Semigroups 
- Monoids (natural numbers, endofunctions, endomorphism of monoids)
- Commutative monoids (a monoid where multiplication/addition operation is commutative, e.g. fake natural numbers `mat`, â„•, â„¤, â„¤[i],  )
- Groups (introduced as an extension of the strucute of monoids (monoids with inverses), e.g. integers and gaussian integers â„¤[i], group of auto-functions (functions `X â†’ X` which are equivalences.))
- Commutative rings (two operations (+, *), e.g. integers and gaussian integers)
-/


/-
In each case, we have seen how to __relate__ objects of the same kind by an appropriate notion of __map__ (also called  __morphism__) between them: 

- Function between types (`f : X â†’ Y`, e.g. `bool_of_nat` relates `nat` to `bool`, `nat_of_bool`, many coercion functions, for instance from integers to Gaussian integers. )
- Pointed functions (i.e. functions which preserved the points of pointed type domain) (`f : (X, x : X) â†’ (Y, y : Y)` where `f x = y`)
- Derivations of propositions (`P â†’ Q` if we can derived `Q`from `P`)
- Graph homomorphism between graphs (they preserve adjacency relation)
- Monoid homomorphism between monoids (they preserve multiplication/addition operation)
- Group homomorphism between groups (they preserve multiplication/addition operation)
- Ring homomorphism between commutative rings (they preserve multiplication and addition operations)
-/ 

/-
Furthermore, we can __compose__ such maps. For instance in the Graph hackathon we composed homomorphisms of graphs and in HW10 you defined the composition of monoid morphisms.

Recall HW10.Q1 
@[simp]
def mult_monoid.morphism.comp (g : M â†’â‚˜* N) (f : L â†’â‚˜* M)  : L â†’â‚˜* N := 
{ to_fun := g âˆ˜ f,
  resp_one := sorry,
  resp_mul := sorry, } 

And in all of the above cases we established an __identity__ function or homomorphism for each object `X` which is netural with respect to composition. 
-/


/-
This common pattern can be generalized to the abstract notion of category_str. A __category_str__ consitsts of

1. a collection of __objects__,
2.  a collection of __morphisms__, (maps between objects)
3. a composition operation whereby we can compose simpler morphisms and build complex morphisms,
4. an operation which provides identiry morphism for each object in the category_str. 

And these data satisfy the axioms of __associativity__ and (left/right) __unitality__. 
-/


/-
Many of the categories one meets in practice have as objects types with some structure attached to them and as have as morphisms functions or homomorphisms (i.e. structure-preserving functions) between them. This is a good model to keep in mind at the outset. 

Therefore, a category_str may be viewed as consisting of objects bearing a certain kind of structure together with morphisms/mappings between such objects preserving that structure. 

For instance, in the rest of the course we shall construct the following categories: 

-- __Graph__ : the category_str of graphs and graph homomorphisms (the objects of this category_str are __simple graphs__, and morphisms are __graph homomorphisms__)
-- __Mon__ : the category_str of monoids and monoid homomorphisms (the objects of this category_str are monoids, and morphisms are monoid homomorphisms)
-- __Group__ : the category_str of groups and groups homomorphisms
-/


/-
However, there are categories which are not the categories of structures. We construct a category_str whose objects are natural numbers `1,2,3, ...` and whose morphisms are matrices. 
-/



/- ## Some Philosophical Remarks 
1. A category is a __system__ which has objects and relations between these objects. The objects do not live in isolation: morphisms bind them together.
2. What is more important is the relations (i.e. morphisms): In fact, two objects are the same if they have the same relations to all other objects. This is known as the __Yoneda Lemma__ which we shall discuss in the last lecture.  Therefore, an object in a category is fully determined by its relations to other objects. Note that this is a not necessarily true for other systems of objects and relations. Is it true for instance than an animal is fully determined by the total sum of its relation to all other animals (including oneself, c.f. being vs becoming).   
-/


/- 
A preliminary ad-hoc structure on the way to defining the category structure, containing only the data of hom-types, the operations of identity and composition. 
Later, we extend the structure of precategory_str to category_str.
-/

--library_note "category_str_theory universes"

/-
In the mathematical language Lean, types are organized into a hierarchy of universe levels, with each level representing a different __universe__ of types that are considered to be of a certain size or complexity. This hierarchy allows for a more structured and well-defined approach to working with types, while __avoiding the inconsistencies and paradoxes that can arise from allowing for the existence of a type of all types__.

It is possible to define a type in Lean that represents the collection of all types, but this is done in a more carefully __controlled__ manner than simply allowing for the existence of a type of all types. Universe levels are consistency control parameters. 
-/
universes v u -- this handles the distinction between small and large categories -- universe variables are inserted in the order that they were declared.




class precategory_str (obj : Type u) : Type (max u (v+1))  :=
(hom : obj â†’ obj â†’ Type v) -- for any two objects `X : obj` and `Y : obj` we have the type `hom X Y` of morphisms between `X` and `Y` 
(id       : Î  X : obj, hom X X) -- specifies identity morphism for all types 
(comp     : Î  {X Y Z : obj}, (hom X Y) â†’ (hom Y Z) â†’ (hom X Z) )
-- ( id       : Î  X : obj, hom X X )
-- ( comp     : Î  {X Y Z : obj}, (hom Y Z) â†’ (hom X Y) â†’ (hom X Z) )

#check precategory_str
--#print precategory_str



/-! #### notation remarks
There is a special notation for the morphisms in a category_str: if `X Y : C`, we write

-  `X âŸ¶ Y` for the type `hom X Y`  of morphisms from `X` to `Y`.  Note: X âŸ¶ Y is entirely different than the type X â†’ Y of functions from `X` to `Y`.  
  (To enter the special arrow `âŸ¶`, type `\h` or `\hom`, or hover over the symbol to see the hint.)

- `ğŸ™ X` is a the identity morphisms on `X` (i.e., a term of type `X âŸ¶ X`).  (To enter the special arrow `ğŸ™`, type `\b1` or hover over the symbol to see the hint.)

- If `f : X âŸ¶ Y` and `g : Y âŸ¶ Z`, then we write `g âŠš f` for the composition, a morphism `X âŸ¶ Z`. -- this is composition in every category_str, not necessarily in the category_str of types
-/



infixr ` âŸ¶ `:10 := precategory_str.hom -- type as \h
notation `ğŸ™` := precategory_str.id -- type as \b1
-- infixr ` âŠš `:80 := precategory_str.comp-- type as \oo

local notation f ` âŠš `:80 g:80 := precategory_str.comp g f    -- type as \oo



section
variables {ğ“’ : Type} [precategory_str ğ“’]
variables W X Y Z : ğ“’ -- terms of type C can be regarded as objects of precategory_str ğ“’
#check X âŸ¶ Y
variables fâ‚€ fâ‚ : X âŸ¶ Y 
variables gâ‚€ gâ‚ : Y âŸ¶ Z
#check ğŸ™ X
--#check fâ‚€ âŠš gâ‚€ -- this does not type check because the composition goes the other way round.
#check gâ‚€ âŠš fâ‚€ 
--#check fâ‚€ âŠš gâ‚
#check gâ‚ âŠš fâ‚€

end 


/-
- Now, we add the axioms of __unitality__ and __associativity__ to extend the structure of a precategory_str to a category_str. 
- The typeclass `category_str C` describes morphisms associated to objects of type `C`.
-/

class category_str (obj : Type u) extends precategory_str.{v} obj : Type (max u (v+1)) :=
(id_comp' : âˆ€ {X Y : obj} (f : hom X Y), f âŠš (ğŸ™ X)  = f . obviously) -- naming based diagrammatic order of composition
(comp_id' : âˆ€ {X Y : obj} (f : hom X Y), (ğŸ™ Y) âŠš f = f . obviously)
(comp_assoc'   : âˆ€ {W X Y Z : obj} (f : hom W X) (g : hom X Y) (h : hom Y Z),
  (h âŠš g) âŠš f = h âŠš (g âŠš f) . obviously)


#check category_str.id_comp'


set_option trace.simp_lemmas true



instance : category_str â„• := 
{ hom := Î» x, Î» y, plift (x â‰¤ y),
  id := Î» x, âŸ¨ le_refl x âŸ©,
  comp := Î» x y z, Î» f, Î» g, âŸ¨le_trans f.down g.down âŸ©,
  id_comp' := by {intros X Y f, simp},
  comp_id' := by {intros X Y f, simp},
  comp_assoc' := by {intros W X Y Z f g h, simp}, }





/-
`restate_axiom` is a command that creates a lemma from a structure field discarding any auto_param wrappers from the type.
It removes a backtick from the name, if it finds one, and otherwise adds "_lemma".
-/

restate_axiom category_str.id_comp'
restate_axiom category_str.comp_id'
restate_axiom category_str.comp_assoc'

/-
`restate_axiom` automates writing lemmas by hand. If we did not use `restate_axiom` then we had to prove a lemma like in below:

@[simp]
lemma id_comp {C : Type} [category_str C] {X Y : C} : 
  âˆ€  f : X âŸ¶ Y ,  (ğŸ™ X) âŠš f = f  := 
begin
  intro f, 
  rw [category_str.id_comp' f],
end   

At any rate, we now have three lemmas which we can use for rewrite/substitution.
-/

#check category_str.id_comp -- this is a lemma made directly from the field ategory_str.id_comp' in the definition of category.
#check category_str.comp_id
#check category_str.comp_assoc

/-
We add the attributes `simp` so that the tactic `simp` works when using these lemmas to simplify the state of our proofs. 
-/
attribute [simp] category_str.id_comp category_str.comp_id category_str.comp_assoc
attribute [trans] precategory_str.comp




initialize_simps_projections category_str (to_precategory_str_hom â†’ hom,
  to_precategory_str_comp â†’ comp, to_precategory_str_id â†’ id, -to_precategory_str)





/--
A `large_category_str` has objects in one universe level higher than the universe level of
the morphisms. It is useful for examples such as the category_str of types, or the category_str
of groups, etc.
-/
abbreviation large_category_str (C : Type (u+1)) : Type (u+1) := category_str.{u} C
/--
A `small_category_str` has objects and morphisms in the same universe level.
-/
abbreviation small_category_str (C : Type u) : Type (u+1) := category_str.{u} C


namespace category_str

/-! ## Category of Types
There is a large category of types where the objects are types and the morphisms are functions between types. -/
instance cat_of_types : category_str Type* :=
{ 
  hom := Î» X, Î» Y, X â†’ Y,
  id := Î» X, id,
  comp := Î» X Y Z, Î» f, Î» g, g âˆ˜ f,
  id_comp' := by {intros X Y, intro f, refl},
  comp_id' := by {intros X Y, intro f, refl},
  comp_assoc' := by {
                      intros W X Y Z, 
                      intros f g h, 
                      refl,
                      --funext, 
                      --dsimp, 
                      --refl,} 
                    } 
}



#check category_str.cat_of_types.id_comp'

--#reduce category_str.cat_of_types.id_comp'

#reduce category_str.cat_of_types.id_comp' (Î» x, (x + 1))


section 

variables (ğ“’ : Type u) [category_str.{v} ğ“’] (X Y : ğ“’) (f : X âŸ¶ Y) -- let ğ“’ be a category and `f : X âŸ¶ Y` a morphism in it . 

#reduce category_str.id_comp' f

end --section 



/- Note that by the tactic `.obviously` we actually do not need to provide the proofs of three least fields `comp_id'` and  `comp_assoc'` and `comp_assoc'` since all of them follows simply from `refl`. -/ 

#check (ğŸ™ â„•) âŠš (ğŸ™ â„•)




example  : 
  bool_of_nat âŠš (ğŸ™ â„•) = bool_of_nat := 
begin
  simp, 
end 



/-! ## Category of Pointed Types 
See lecture 11 for definitions of `â†’â€¢` and pointed_type.id, or simply command+click below. 
-/ 

instance : large_category_str pointed_type :=
{ 
  hom := Î» X, Î» Y, X â†’â€¢ Y, -- really X is (A, a)
  id := Î» X, pointed_type.id, -- id: (A,a) âŸ¶ (A,a)  
  comp := Î» X Y Z, Î» f, Î» g, g âˆ˜â€¢ f,
}


/-! ##  The Category of a Preorder
Every preorder can be seen as a small category where the objects are the elements/terms of the (underlying type) of the preorder and between two objects `x` and `y` there is a (unique) morphism iff `x â‰¤ y`. To do this we need some tools to see a proposition as the type of its proofs.  
-/

/- We use `plift` to lift a proposition to the type of its proofs -/
#check plift (0 = 0) -- this a type whose terms are proofs of `0 = 0`. 

#check ( âŸ¨ (rfl : 0 = 0) âŸ© : plift (0 = 0) ) -- Since `rfl` is a proof of `0 = 0` we can lift it up to a term `âŸ¨ (rfl : 0 = 0) âŸ©` of type `plift (0 = 0)`. 

-- For a proposition `P`, the terms of type `plift`


/- We use `ulift` to lift a proposition to the type of its proofs -/
#check ulift -- lifting types from one universe to a higher universe
#check plift -- lifting propositions to types



instance small_cat_of_preorder (X : Type) [preorder X] : small_category_str X := 
{
  hom := Î» x, Î» y, (plift (x â‰¤ y) : Type), 
  id := Î» x, âŸ¨ le_refl x âŸ©, 
  comp := Î» x y z, Î» f, Î» g, âŸ¨le_trans f.down g.down âŸ©,
}

def two_to_three :  1 âŸ¶ 2 := 
âŸ¨one_le_twoâŸ© 

#check two_to_three 

#reduce category_str.comp_id' two_to_three 


#check category_str.small_cat_of_preorder

instance foo : small_category_str â„• := 
category_str.small_cat_of_preorder â„• 

#check category_str.foo.hom 2 3

#reduce category_str.foo.hom 2 3




section lifting_categories

variables (ğ“’ : Type u)[category_str.{v} ğ“’]


universe u'
-- we can lift ğ“’ from universe `u` to a higher universe `u'`. 
instance ulift_cat : category_str.{v} (ulift.{u'} ğ“’) :=
{ hom  := Î» X Y, (X.down âŸ¶ Y.down),
  id   := Î» X, ğŸ™ X.down,
  comp := Î» _ _ _ f g, g âŠš f }

-- We verify that this previous instance can lift small categories to large categories.
example (ğ“¢ : Type u) [small_category_str ğ“¢] : large_category_str (ulift.{u+1} ğ“¢) := 
by apply_instance

end lifting_categories



/-
+ There are many more categories which we shall introduce in the three remaining lectures: the category of graphs, the category of monoids, the category of groups, the category of rings, the category of vector spaces, and finally the category of categories! 

+ There are statements which are true in all these categories by virtue of  being a category. This is like saying for instance a city has a town-hall by virtue of being a city (that is, we don't know in which country that city is located, what is the population of that city, etc. we just know it is a city.) 

+ It is interesting to see which statements are true in an arbitrary category. If we construct something or prove a statement in an arbitrary category,then these constructions and statements and will be valid in every particular category, such as the category of types, groups, etc.  

+ In below, we shall introduce new definitions and prove statements which are valid in an arbitrary category. 
-/


variables {ğ“’ : Type u} [category_str ğ“’] {W X Y Z : ğ“’} {A : Type}



/-! # Delooping of a monoid 
Given a monoid `M` (i.e. a type equipped with a monoid structure), we construct a category which has only one object and `M` many morphisms. The composition of morphisms in this category is given by the monoid multiplication. 
-/


instance delooping (M : Type u)[mult_monoid_str M] : small_category_str.{u} (punit : Type u) := 
{ 
  hom := Î» _, Î» _, M,
  id := 1,
  comp := Î» _ _ _, (*),
  id_comp' := by {intros _ _ _, simp [mult_mon_one_mul], },
  comp_id' := by {intros _ _ _, simp [mult_mon_mul_one],},
  comp_assoc' := by {intros _ _ _ _ _ _ _,simp,}, 
}  

#check category_str.delooping
#check category_str.comp




-- A shorter proof
-- instance delooping (M : Type)[mult_monoid_str M] : small_category_str unit := 
-- { 
--   hom := Î» _, Î» _, M,
--   id := 1,
--   comp := Î» _ _ _, (*),
--   id_comp' := by {simp},
--   comp_id' := by {simp},
--   comp_assoc' := by {intros _ _ _ ,simp}, 
-- }  


-- Even shorter using tactic `.obviously`

instance delooping_alt (M : Type)[mult_monoid_str M] : small_category_str unit := 
{ 
  hom := Î» _, Î» _, M,
  id := 1,
  comp := Î» _ _ _, (*),
}  





/-
Conversely, in every category every object has, by virtue of being an object of a category, a monoid structure.  
-/

/- The type of __endomorphisms__ of an object X in category ğ“’ -/

def End (X : ğ“’) := X âŸ¶ X  

#check @End


#check mult_monoid_str

/- The __endomorphisms monoid__ of an object in a category-/
def monoid_of_object {ğ“’ : Type}[small_category_str ğ“’] (X : ğ“’) : mult_monoid_str (X âŸ¶ X) :=  
sorry 





/-! ## Challenge: 
The endomorphisms monoid of the only object in `single_obj Î±` is equivalent to the original
     monoid Î±. -/
-- def to_End {M : Type} [mult_monoid_str M] : M â‰ƒ* End (_) :=
-- sorry







lemma eq_comp {f g : X âŸ¶ Y} (e : f = g) (h : Y âŸ¶ Z) : 
  h âŠš f = h âŠš g :=
begin
-- we want to prove  `h âŠš f = h âŠš g`
  rw e, -- we sub `f` for `g`
end 

  

lemma comp_eq (f : X âŸ¶ Y) {g h : Y âŸ¶ Z} (e : g = h) : 
  g âŠš f = h âŠš f :=
begin
  rw e, 
end 


example (f : W âŸ¶ X) (g : X âŸ¶ Y) (h : Y âŸ¶ Z) :  
(h  âŠš  (ğŸ™ Y âŠš g)) âŠš  f = h âŠš (g âŠš f) :=
begin
  simp, 
end




structure equiv (X Y : ğ“’) :=
(to_mor    : X âŸ¶ Y)
(inv_mor   : Y âŸ¶ X)
(left_inv  : to_mor âŠš  inv_mor = (ğŸ™ Y) ) 
(right_inv : inv_mor âŠš to_mor = (ğŸ™ X)  )


local notation ` â‰… `:85 := equiv



/-
A morphism `f` is an __isomorphism__ if it has both a left inverse and a right inverse.
-/

def is_iso (f : X âŸ¶ Y) := 
âˆƒ (g h : Y âŸ¶ X), (g âŠš f = ğŸ™ X) âˆ§ (f âŠš h = ğŸ™ Y)


/- ## Challenge:
Show that an instance of equivalence `f : X â‰ƒ Y` gives rise to a pair of isomorphisms, i.e. both `f.to_mor` and `f.inv_mor` are isomorphisms.

Conversely, we can prove that every isomorphism gives rise to an equivalence. 
-/



/--
A morphism `f` is a __monomorphism__ if it can be cancelled when __postcomposed__:
`g â‰« f = h â‰« f` implies `g = h`.
-/
@[simp] 
def is_mono (f : X âŸ¶ Y) :=
âˆ€ (W : ğ“’) (xâ‚ xâ‚‚ : W âŸ¶ X), (f âŠš xâ‚ = f âŠš xâ‚‚) â†’  (xâ‚ = xâ‚‚)

@[simp]
lemma cancel_mono {f : X âŸ¶ Y} (mf : is_mono f) {xâ‚ xâ‚‚ : W âŸ¶ X} : 
  (f âŠš xâ‚  = f âŠš xâ‚‚) â†” xâ‚ = xâ‚‚ :=
begin
  split,
  {
    intro h,
    apply mf, 
    exact h,
  },
  {
    apply congr_arg,
  },
end 




/-
Dually, a morphism `f` is an __epimorphism__ if it can be cancelled when __precomposed__:
`g âŠš f = h âŠš f` implies `g = h`.
-/

def is_epi (f : X âŸ¶ Y) := 
âˆ€ {Z : ğ“’} (g h : Y âŸ¶ Z), (g âŠš f = h âŠš f) â†’ g = h




/- ## Challenge 
Show that every isomorphism is a monomorphism. 
-/



/-! ## Opposite Category 
If `ğ“’` is a category, then `ğ“’áµ’áµ–` is the __opposite category__, with objects the same but all arrows reversed. `ğ“’áµ’áµ–` is the mirror image of `ğ“’`. If `X âŸ¶ Y âŸ¶ Z` are morphisms in `ğ“’áµ’áµ–` then `Z âŸ¶ Y âŸ¶ X`  are maps in `ğ“’`. 

In below we give `ğ“’áµ’áµ–` the structure of a category. See `opposite_cat`. 
-/

def opposite (ğ“’ : Type u) : Type u := ğ“’


notation X `áµ’áµ–`:std.prec.max_plus := opposite X


/- The canonical map `ğ“’ â†’ ğ“’áµ’áµ–`. 
We need to write `op X` to explicitly move `X` to the opposite category-/
@[pp_nodot]
def op : ğ“’ â†’ ğ“’áµ’áµ– := id 



/- The canonical map `ğ“’áµ’áµ– â†’ ğ“’`. -/
@[pp_nodot]
def unop : ğ“’áµ’áµ– â†’ ğ“’ := id

section test 
variable XX : ğ“’ 
#check op XX 
#check unop (op XX)

example  : 
  unop (op XX) = XX := rfl 

end  test

@[simp] 
lemma op_unop (X : ğ“’áµ’áµ–) : op (unop X) = X := rfl

@[simp] 
lemma unop_op (x : ğ“’) : unop (op X) = X := rfl


/- The type-level equivalence between a type and its opposite. -/
def equiv_to_opposite :  ğ“’ â‰… ğ“’áµ’áµ– :=
{ 
  to_fun := op,
  inv_fun := unop,
  left_inv := by {ext, refl, },
  right_inv := by {ext, refl, }, 
}


instance opposite_cat {ğ“’ : Type u} [category_str.{v} ğ“’] : category_str.{v} ğ“’áµ’áµ– :=
{ 
  hom := Î» X, Î» Y, (unop Y âŸ¶ unop X), -- informally, hom_{ğ“’áµ’áµ–} X Y = hom_{ğ“’} Y X
  id := Î» X, ğŸ™ (unop X),
  comp := Î» X Y Z, Î» f g, f âŠš g,
  id_comp' := by {intros X Y f, simp,},
  comp_id' := by {intros X Y f, simp,},
  comp_assoc' := by {intros W X Y Z f g h, rw [comp_assoc'],} 
}




/-
The opposite of an arrow in `ğ“’`.
-/
def hom.op  {X Y : ğ“’} (f : X âŸ¶ Y) : 
op Y âŸ¶ op X := f

/-
Given an arrow in `ğ“’áµ’áµ–`, we can take the "unopposite" back in `ğ“’`.
-/
def hom.unop {X Y : ğ“’áµ’áµ–} (f : X âŸ¶ Y) : 
unop Y âŸ¶ unop X := f


@[simp] 
lemma op_comp {X Y Z : ğ“’} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} :
  hom.op (g âŠš f) = hom.op f âŠš hom.op g := 
begin 
  refl, 
end   

@[simp] 
lemma unop_comp {X Y Z : ğ“’áµ’áµ–} {f : X âŸ¶ Y} {g : Y âŸ¶ Z} :
  hom.unop (g âŠš f) = hom.unop f âŠš hom.unop g := 
begin 
  refl, 
end   



@[simp] 
lemma op_id {X : ğ“’} : hom.op (ğŸ™ X) = ğŸ™ (op X) := 
begin
  refl, 
end 

@[simp] 
lemma unop_id {X : ğ“’áµ’áµ–} : hom.unop (ğŸ™ X) = ğŸ™ (unop X) := 
begin
  refl, 
end 




end category_str 







