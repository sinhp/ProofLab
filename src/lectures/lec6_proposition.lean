/- Copyright (c) 2022 Sina Hazratpour. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
----------------

# Logic of Propositions 
Sina Hazratpour
Introduction to Proof  
MATH 301, Johns Hopkins University, Fall 2022   
-/



/- # New tactics unveiled in this lesson:
12. `split` (for introduction of `‚àß`)
13. `cases` (for elimination of `‚àß`)
14. `assumption` (for using a proof from context of assumptions)
15. `intro` (for introduction of `‚Üí`)
16. `apply` (for backward elimination of `‚Üí`)
17. `have` (for introducing auxiliary intermediate goals)
18. `left` (for introduction of `‚à®`)
19. `right` (for introduction of `‚à®`)
20. `suffices`
-/



/- # Goals of this lecture: 
1. Identify techniques for proving a proposition based on its logical structure.
2. Recognize and apply standard proof techniques, including proof by negation, proof by contraposition, and proof by contradiction. 
3. Learn to simplify your proofs.
4. Do all of the above using interactive Lean tactics.  
-/


import ..prooflab
--import .arithmetic_with_tactics
import .lec5_function_composition -- importing is transitive, hence we have already imported proofs1 and proofs0.

set_option pp.beta true
set_option pp.generalized_field_notation false
-- set_option pp.all true

namespace PROOFS



/-! ## Propositions 
Reminder: We declare a proposition by `P : Prop`. 
For `P : Prop`, we read `hp : P` as "hp is a proof of P", or "the hypothesis P verified by hp". 
-/

/-
We introduce global variables; in below P and Q are accessible throughout the entire file, and any file which imports this file.
-/
variables P Q R : Prop -- let `P`, `Q` and `R` be propositions. 


/- 
Lean's "check" command tells us 
the type of the object we have constructd. 
-/
#check P 
#check P ‚àß Q -- conjunction 
#check P ‚à® Q -- disjunction 
#check P ‚Üí Q -- implication 
#check ¬¨ P -- negation 


/-! ## New Propositions from the Old 

Using simple (aka __atomic__) propositions such as ‚ÄúThe sun is shining‚Äù and ‚ÄúIt is raining‚Äù we can form the more complicated proposition ‚ÄúThe sun is shining and It is raining‚Äù.

That required us to first unquote the sentences, insert an ‚Äúand‚Äù and then put a quote around the resulting sentence.

In this way, we can make new propositions from old propositions using __connectives__ (such as ‚Äúand‚Äù/conjunction, "if ... then"/implication, "or"/disjunction). For each connective, we specify a __rule__ to __introduce__ a proof of the compound proposition using that connective (the so-called introduction rule) and a rule to __use__ or ___eliminate__ such a proof.

We call a compound proposition (such as ‚ÄúThe sun is shining and It is raining‚Äù) a __propositional formula__. 
-/


/-! ### Conjunction (and) 
We use the tactic __split__ in order to prove a proposition formula of the form `P ‚àß Q`. 

    P   Q
   -------- ‚àß-intro
    P ‚àß Q 

We use the tactic __cases__ in order to use a proof of `P ‚àß Q` to prove some other proposition. The tactic `cases` replaces `h : P ‚àß Q` by a pair of assumptions, `hp : P` and `hq : Q`. 

    P ‚àß Q
   -------- ‚àß-elim_left
      P 

    P ‚àß Q
   -------- ‚àß-elim_right
      Q 


-/

-- introduction example 
example (hp : P) : P ‚àß P := 
begin
  split, 
  { 
    -- the proof of the first subgoal which is `P` 
    exact hp, 
  },
  {
    -- the proof of the second subgoal which is `P` 
    exact hp, 
  },
end 




example (hp : P) : P ‚àß P := 
begin
  split, 
  repeat {exact hp},
end 




-- elimination example 
lemma fst_prop (h : P ‚àß Q) : 
  P := 
begin 
  cases h,  
  exact h_left, 
end 




-- elimination example 
lemma fst_prop_alt (h : P ‚àß Q) : 
  P := 
begin 
  cases h with h‚ÇÅ h‚ÇÇ,  
  exact h‚ÇÅ,  
end 



--elimination example 
lemma snd_prop (h : P ‚àß Q) : 
  Q := 
begin 
  cases h with h‚ÇÅ h‚ÇÇ,  
  exact h‚ÇÇ,  
end 





/- the following is an example of both intro and elim rules: we need intro rule to construct a proof of `Q ‚àß P` and we need elim rule to be able to use `h : P ‚àß Q` -/
lemma conjunction_swap (h : P ‚àß Q) : 
  Q ‚àß P := 
begin
  split, 
  {
    -- we need to prove `Q`. 
    -- we are trying to use `h` to prove `Q` holds. 
    cases h with sheep goat,
    exact goat, 
  },
  {
   -- we need to prove `P`. 
    -- we are trying to use `h` to prove `Q` holds. 
    cases h with sheep goat,
    exact sheep,  
  }, 
end   


#check conjunction_swap



lemma conjunction_swap_alt (h : P ‚àß Q) : 
  Q ‚àß P := 
begin
  cases h with hp hq, -- eliminating h to get proofs of `P` and `Q`. 
  split,  -- we want to introduce a proof of conjunction 
  {
    exact hq, 
  },
  {
    exact hp, 
  },
end   



------------------------
-- tactic __assumption__ 
------------------------
/-
The `assumption` tactic looks through the assumptions in the context of the current goal, and if there is one matching the conclusion, it applies that asssumption. Here is an example:
-/

/- 
This one is the same as the last proof but more parsimonous: we use `repeat {assumption}` instead of many instances of `exact`.  
-/


lemma conjunction_swap_alt_alt (h : P ‚àß Q) : 
  Q ‚àß P := 
begin
  cases h with hp hq, -- eliminating h to get proofs of `P` and `Q`. 
  split, 
  {
    assumption, -- this tactic means you are instructing Lean to go and find the proof of the goal from the context of assumption, only if the exact proof exists in the context. 
  },
  {
    assumption, 
  },
end   



lemma conjunction_swap_alt_alt_alt (h : P ‚àß Q) : 
  Q ‚àß P := 
begin
  cases h with hp hq, -- eliminating h to get proofs of `P` and `Q`. 
  split, 
  repeat {assumption}, 
end   


lemma conjunction_swap_alt_alt_alt_alt (h : P ‚àß Q) : 
  Q ‚àß P := 
begin
  split, 
  repeat { cases h, assumption}, 
end   




-- introduction example : complete the proof below using only `assumption` and `exact` tactics. 
example (hp : P) (hq : Q) (hr : R) : 
  (P ‚àß Q) ‚àß (P ‚àß R) :=
begin
  split, 
  {
    split, 
    {
      assumption, -- or exact hp
    },
    {
      assumption, 
    },
  },
  {
    split, 
    {
      assumption,
    },
    {
      assumption,
    },
  },
end 


-- Challenge: make it shorter with `repeat`. 






example (hp : P) (hq : Q) (hr : R) : 
  (P ‚àß Q) ‚àß (P ‚àß R) :=
begin
  split, 
  {
    split, 
      repeat {assumption},
  },
  {
    split, 
    repeat {assumption},
  },
end 







/-! ### Implication (If ... then ...) 
The implication `P ‚Üí Q` is a new proposition built from P and Q and we read it as ‚Äúif P then Q‚Äù.

Implicaiton __introduction__:
If under the assumption that `P` is true we can derive that `Q` is true, then we know that `P ‚Üí Q` is true. Note that, in this process, we actually do not know whether P is true or not; all we know is that in case `P` holds, then so does `Q`. To assume `P` first we introduce a hypothetical proof `hp` of `P`, and we use `hp` to construct a proof of `Q`, and thereby show that `Q` holds under the assumption that `P` holds. 

    P
    .
    .
    .
    Q
--------- (‚Üí intro)
  P ‚Üí Q


Implication __elimination__: 
Application of a proof 
`h : P ‚Üí Q`
to a proof 
`p : P`  
is achieved by the expression
`h p`  
that is `h` followed by `p`.

This is (like) function application.


  P ‚Üí Q   P 
------------- (‚Üí elim)
      Q



If `h` and `p` are compound expressions, we put brackets around them to make it clear where each one begins and ends. 
e.g. `h‚ÇÅ h‚ÇÇ h‚ÇÉ` is interpreted by Lean as `(h‚ÇÅ h‚ÇÇ) h‚ÇÉ`.
-/


--introduction example
example : 
  P ‚Üí P := 
begin 
  intro hp, -- this is the assumption that `P` holds. our goal changes 
  exact hp, 
end 

example : 
false ‚Üí false := 
begin 
  intro hp, 
  exact hp, 
end 


example : 
  false ‚Üí true := 
begin
-- an example of this is the proposition (0 = 1) ‚Üí (1 = 1)
intro hp, 
exact trivial, -- trivial is the unique proof of proposition `true`, therefore `true` is true. 
end 


--introduction example
lemma conjunction_commutative :
  P ‚àß Q ‚Üí Q ‚àß P := 
begin
  intro hpq, 
  -- we want to use the elim rule of conjunction (`‚àß`) to construct a proof of `P` and a proof of `Q` to use them later. 
  cases hpq with hp hq,
  -- we want to use the intro rule of conjunction to prove our goal `Q ‚àß P` 
  split,
  {
    -- well, we know that `Q` is true and the proof of that is `hq`. 
    exact hq,
  },
  {-- well, we know that `P` is true and the proof of that is `hp`. 
    assumption, -- note that `hp` was introduced later in the proof
  },
end   




--Alternatively, we can use the lemma `conjunction_swap` which we proved before: 
example : 
  P ‚àß Q ‚Üí Q ‚àß P := 
begin
-- the second line of the following proof uses the elimination rule of implication which we have not learned yet, but we will in about five minutes.  
  intro hpq, 
  exact conjunction_swap P Q hpq, 
end 



 -- an example of implication introduction 
example (n : ‚Ñï) : 
  (0 = 1) ‚Üí (0 = n) := 
begin 
intro h‚ÇÅ, 
calc 0 = 0 * n : by rw zero_mul
... = 1 * n : by rw h‚ÇÅ
... = n : by rw one_mul,
end 



/- Next examples constructs a proof of `(0 = 1) ‚Üí (0 = n)`. -/

lemma nat_mul_congr (x y z : ‚Ñï) (h : x = y) : 
  x * z = y * z := 
begin
  rw h,
end 
 


-- #check nat_mul_congr

------------------------
-- tactic __have__ 
------------------------

/- `have` is a good tactic to use, if you want to add a new intermediate subgoal which, after proving it, can be used later as a new assumption in the (updated) context. 

There are two styles of using the tactic `have`.

- The term-style: 
` have hp : P, from ...  `


- The tactic-style: 
` have hp : P, by {tactic_1 ..., tactic_2 ...,  tactic_n ... } `

In the first style, we are introducing a new assumption `hp` (a proof of proposition `P`) from another proof (`...`) that we know how to construct.   
-/



example (n : ‚Ñï) : 
  (0 = 1) ‚Üí (0 = n) := 
begin
  -- we want to prove an implication, hence we use the implication introduction rule. 
  intro h‚ÇÅ, 
  -- under the hypothetical assumption that `0 = 1` we need to prove that `0 = n`.  To prove this we use the lemma `nat_mul_congr` in the following way: we use the lemma by using x to be `0` and `y` to be `1` and `z` to be `n` 
  -- We use the tactic `have` to add a new assumption `h‚ÇÇ` which proves that `0 * n = 1 * n` from the lemma `nat_mul_congr` applied to arguments `0 1 n h‚ÇÅ`. 
  have h‚ÇÇ : 0 * n = 1 * n, from nat_mul_congr 0 1 n h‚ÇÅ, 
  -- this proves ` 0 * n = 1 * n` 
  -- use h‚ÇÇ to prove `0 =n` 
  rw [zero_mul, one_mul] at h‚ÇÇ, 
  assumption,
end 



example (n : ‚Ñï) : 
  (0 = 1) ‚Üí (0 = n) := 
begin
  intro h‚ÇÅ, 
  -- now under the hypothetical assumption that `0 = 1` we need to prove that `0 = n`.  
  have h‚ÇÇ : 0 * n = 1 * n, from nat_mul_congr 0 1 n h‚ÇÅ, 
  -- we add a new assumption that `0 = 0 * n` using the tactic-style `have` and the lemma `zero_lemma`. 
  have h‚ÇÉ : 0 = 0 * n, by {rw zero_mul}, 
  -- we add a new assumption that `n = 1 * n` using the tactic-style `have` and the lemma `one_mul`. 
  have h‚ÇÑ : n = 1 * n, by {rw one_mul}, 
  rw ‚Üê h‚ÇÉ at h‚ÇÇ,  
  rw ‚Üê h‚ÇÑ at h‚ÇÇ,  
  show 0 = n, from h‚ÇÇ, 
end 




example (n : ‚Ñï) : 
  (0 = 1) ‚Üí (0 = n) := 
begin 
intro h‚ÇÅ, 
calc 0 = 0 * n : by rw zero_mul
... = 1 * n : by rw h‚ÇÅ
... = n : by rw one_mul,
end 



example (n : ‚Ñï) : 
  (0 = 1) ‚Üí (0 = n) := 
begin
  intro h, 
  simp at *, 
  exfalso, 
  assumption,
end 







--Alternatively, we can use the lemma `conjunction_swap` which we proved before: 
example : 
  P ‚àß Q ‚Üí Q ‚àß P := 
begin
  intro h‚ÇÅ, 
  exact conjunction_swap P Q h‚ÇÅ, 
end 





example : 
  P ‚Üí Q ‚Üí P :=
begin
-- we want to prove `P ‚Üí (Q ‚Üí P)` so we use the intro rule of `‚Üí`
intro h‚ÇÅ, 
-- we are proving an implication, hence we use the intro rule of ‚Üí 
intro h‚ÇÇ, 
-- we know `P` is true by `h‚ÇÅ`. 
assumption,
end 




example : 
  P ‚Üí Q ‚Üí P ‚àß Q :=
begin
  sorry, 
end 





-- Application elimination exmaple aka Modus Ponens
lemma modus_ponens : 
  P ‚Üí (P ‚Üí Q) ‚Üí Q := 
begin   
  intros hp h, 
  exact h hp,
end 



-- Depenedent Modus Ponens
lemma dep_modus_ponens: 
  P ‚Üí (P ‚Üí P ‚Üí Q) ‚Üí Q :=
begin
  -- we want to prove P ‚Üí ((P ‚Üí(P ‚Üí Q)) ‚Üí Q) so we use intro rule of ‚Üí 
  intro h‚ÇÅ, 
  -- we want to prove (P ‚Üí(P ‚Üí Q))  ‚Üí Q so we use intro rule of ‚Üí 
  intro h‚ÇÇ, 
  --  we want to Q; we create a new subgoal P ‚Üí Q, and prove it using tactic `have`
  have h‚ÇÉ : P ‚Üí Q, from h‚ÇÇ h‚ÇÅ, 
-- we use `h‚ÇÉ` to prove `Q` by application elimination. 
  exact h‚ÇÉ h‚ÇÅ, 
end 


-- Depenedent Modus Ponens
example : 
  P ‚Üí (P ‚Üí P ‚Üí Q) ‚Üí Q :=
Œª h‚ÇÅ, Œª h‚ÇÇ, (h‚ÇÇ h‚ÇÅ) h‚ÇÅ  




/-
Transitivity of implications: If we know that  proposition P implies Q and Q implies R then we know that  P implies R. 
-/

theorem transitivity_of_implication_1 :
  (P ‚Üí Q) ‚Üí (Q ‚Üí R) ‚Üí P ‚Üí R :=
begin
  sorry, 
end   

/-
Another style to write the same proof above by using the tactic 
have ..., from ...
-/

theorem transitivity_of_implication_2 :
  (P ‚Üí Q) ‚Üí (Q ‚Üí R) ‚Üí P ‚Üí R :=
begin   
  sorry, 
end 

example : 
  (P ‚Üí (Q ‚Üí R)) ‚Üí (P ‚àß Q ‚Üí R) :=
begin 
  sorry, 
end 



------------------------
-- tactic __apply__ 
------------------------
/- The `apply` tactic takes a proof of a general statement or implication, tries to match the conclusion with the current goal, and leaves the hypotheses, if any, as new goals. If the given proof matches the goal exactly (modulo definitional equality), you can use the exact tactic instead of apply. -/

example (hpq : P ‚Üí Q) (hqr : Q ‚Üí R) (hp : P) : 
  R :=
-- We prove `R` using backward reasoning as follows.  
begin
-- To prove `R`, by hypothesis `hqr` it suffices to prove `Q`.
  apply hqr,
-- To prove `Q`, by hypothesis `hpq` it suffices to prove `P`.  
  apply hpq,
-- To prove `P` we use hypothesis `hp`. 
  apply hp,
end


section 
variables a b c : ‚Ñù
#check (le_refl a : a ‚â§ a)
#check (le_trans : a ‚â§ b ‚Üí b ‚â§ c ‚Üí a ‚â§ c)
#check (le_trans : a ‚â§ b ‚Üí b ‚â§ c ‚Üí a ‚â§ c)
end 


example (x y z : ‚Ñù) (h‚ÇÄ : x ‚â§ y) (h‚ÇÅ : y ‚â§ z) :
  x ‚â§ z := 
begin
exact le_trans h‚ÇÄ h‚ÇÅ,
end 


example (x y z : ‚Ñù) (h‚ÇÄ : x ‚â§ y) (h‚ÇÅ : y ‚â§ z) :
  x ‚â§ z := 
begin
-- our goal is to prove `x ‚â§ z`. After using `apply le_trans` Lean is searching the context to find some `y` with `x ‚â§ y` and `y ‚â§ z` so that it can apply `le_trans` to them. 
  apply le_trans,
-- the goals are ‚ä¢ x ‚â§ ?m_1 and ?m_1 ‚â§ z : We solve the first goal using `h‚ÇÄ`. One `h‚ÇÄ` is supplied, `?m_1` is replaced by `y`. 
  apply h‚ÇÄ,
-- ‚ä¢ the only remaining goal is  `y ‚â§ z`. 
  apply h‚ÇÅ,
-- alternatively, we could have used `exact h‚ÇÅ` as usual.   
end



/- 
let's do curry fully tactic style; we use our new tactic `apply`.
-/
-- a tautology is proposition which is always true. 
lemma curry_prop : 
  (P ‚àß Q ‚Üí R) ‚Üí (P ‚Üí Q ‚Üí R) :=
begin 
  intro h‚ÇÅ, -- we want to prove the implication (P ‚àß Q ‚Üí R) ‚Üí (P ‚Üí Q ‚Üí R) hence we use the introduction rule of implication. 
  intro h‚ÇÇ, -- we want to prove the implication P ‚Üí (Q ‚Üí R), hence we use the introduction rule of implication. 
  intro h‚ÇÉ,  -- we want to prove Q ‚Üí R, hence we use the introduction rule of implication. 
  apply h‚ÇÅ, -- we want to show R hence by backward proving we need to supply a proof of P ‚àß Q
  split, -- we want to prove P ‚àß Q and therefore we use the introduction rule of conjunction 
  repeat {assumption},
end 


example : 
  (P ‚àß Q ‚Üí R) ‚Üí (P ‚Üí Q ‚Üí R) :=
begin
itauto,
end   


example : 
  (P ‚àß Q ‚Üí R) ‚Üí (P ‚Üí Q ‚Üí R) :=
begin
itauto,
end   



example : 
  (P ‚àß Q ‚Üí R) ‚Üí (P ‚Üí Q ‚Üí R) :=
begin
exact and_imp.mp,
end  



#check curry_prop
#check curry 


lemma curry_prop_fun : 
  (P ‚àß Q ‚Üí R) ‚Üí (P ‚Üí Q ‚Üí R) :=
Œª h, Œª hp, Œª hq, h ‚ü®hp, hq‚ü©   

#check curry_prop_fun



example : 
  (P ‚Üí (Q ‚Üí R)) ‚Üí (P ‚àß Q ‚Üí R) :=
begin 
  sorry, 
end 



/-
The proof below shows us why we prefer backward reasoning with `apply`. A forward reasoning proof would be more complicated 
-/

example : (((P ‚Üí Q)  ‚Üí Q) ‚Üí Q) ‚Üí P ‚Üí Q :=
begin 
  sorry, 
end






/-! ### Biimplication, or otherwise known as If and Only If 
The biimplication ` ‚Üî ` is a derived connective which is derived form `‚Üí` and `‚àß`. For propositions `P` and `Q`, we write `P ‚Üî Q` for `(P ‚Üí Q) ‚àß (Q ‚Üí P)`. Therefore `P ‚Üî Q = (P ‚Üí Q) ‚àß (Q ‚Üí P)` by definition. And, as such, we can apply the tactic `split` to introduce a proof of `P ‚Üî Q` and `cases` to eliminate proofs of `P ‚Üî Q`.
-/

/-
**Note** The `constructor` tactic applies the unique constructor for conjunction, namely `and.intro`. 
-/

lemma conj_comm : P ‚àß Q ‚Üî Q ‚àß P :=
begin
  split, 
  {
    sorry, 
  },
  {
    sorry, 
  },
end



example : 
P ‚àß Q ‚àß R ‚Üí R ‚àß Q ‚àß P := 
begin 
    sorry, 
end 





lemma disj_comm {P Q : Prop} : 
  P ‚à® Q ‚Üî Q ‚à® P :=
begin
  sorry, 
end

#check disj_comm
#check disj_comm.mp -- extract the forward direction implication `P ‚à® Q ‚Üí Q ‚à® P` from the proof `disj_comm` by using `.mp` notation.  
#check disj_comm.mpr -- extract the converse implication `Q ‚à® P ‚Üí P ‚à® Q` from the proof `disj_comm` by using `.mpr` notation.  







theorem conjunction_distributes_over_disjunction : 
  P ‚àß (Q ‚à® R) ‚Üî (P ‚àß Q) ‚à® (P ‚àß R) :=
begin
  sorry, 
end


#check conjunction_distributes_over_disjunction
#check (conjunction_distributes_over_disjunction P Q R).mp
#check (conjunction_distributes_over_disjunction P Q R).mpr


#check eq_zero_or_eq_zero_of_mul_eq_zero

example (a b : ‚Ñù) : 
  a * b = 0 ‚Üî (a = 0) ‚à® (b = 0) :=
begin
  sorry, 
end   





/- 
We say two propositions `P` and `Q` are __equivalent__ if we can give a proof of `P ‚Üî Q`. 
-/

/-
We prove that if propositions `P` and `Q` are equivalent then `A ‚àß P` and `A ‚àß Q` are equivalent for any proposition `A`. 
-/ 


lemma conj_respects_eqv {A P Q : Prop} : 
  A ‚àß P ‚Üî A ‚àß Q := 
begin
  sorry, 
end    

-- Todo: prove similar lemmas for disjunction, implication, and negation 



lemma disj_respects_eqv {A P Q : Prop} : 
  A ‚à® P ‚Üî A ‚à® Q := 
begin
  sorry, 
end



def is_eqv (P Q : Prop) := P ‚Üî Q 

#check is_eqv


example {A B : Prop} (hPQ : is_eqv P Q) :
  A ‚àß (P ‚à® B) ‚Üî A ‚àß (Q ‚à® B) :=   
begin
  sorry, 
end 
-- What is the general statement here? 


#check and
#check and P Q 

def and_curry : Prop √ó Prop ‚Üí Prop := 
Œª Œ± , (Œ±.1 ‚àß Œ±.2)


#check copy 
#reduce copy 

example : 
  is_eqv (and_curry (copy P)) P := 
begin
unfold copy, 
unfold and_curry,
dsimp, 
split, 
  {
    intro h, 
    cases h with h‚ÇÅ h‚ÇÇ, 
    exact h‚ÇÅ,    
  },
  {
    intro h, 
    split, 
    assumption',
  },  
end 

#check is_eqv ‚àò and_curry ‚àò copy 


-- We say that two functions `f: Prop ‚Üí Prop` and `g: Prop ‚Üí Prop` are pointwise equivalent if for any propostion `P`  


def is_eqv_ptw (P : Prop) (f g : Prop ‚Üí Prop) := 
is_eqv (f P) (g P) 

#check is_eqv_ptw




/-! ### Falsity
Sometimes we need to work with propositions which are always false such as ‚ÄúA bachelor is married‚Äù, or "0 = 1". In Lean, we denote the false proposition by `false`. The universal property of a false proposition is that any other proposition follows from the false proposition. We can prove any proposition from `false`. This is known as the __principle of explosion__ üéÜ aka ex falso. 
-/

example (P : Prop) (h : false) :
  P :=
begin
  exfalso, 
  exact h, 
end   
 
example : 
  false ‚Üí P := 
begin
  intro h,  
  exfalso,
  exact h, 
end


/-! ### True
Sometimes we need to work with propositions which are always true such as ‚ÄúA bachelor is unmarried‚Äù, or "0 = 0". In Lean, we denote the false proposition by `true`. The universal property of a false proposition is that any other proposition entails the true proposition. In fact, `true` has a unique proof, namely `trivial`.  
We can prove any proposition from `false`. This is known as the __principle of explosion__ üéÜ aka ex falso. 
-/

example : 
  true := 
begin
  exact trivial,
end   

example : false ‚Üí true ‚Üí Q ‚Üí P ‚Üí Q   :=
begin 
intro u, 
exfalso,
exact u, 
end






/-! ### Negation  
If we start with a propositon `P`, the negation `¬¨P` (aka "not P") is _defined_ by the formula `P ‚Üí false`, which you can think of as saying that `P` implies something impossible (`false`). Therefore, if `¬¨ P` is the case, then `P` cannot be the case, since if `P` were the case, we would conclude that something false/impossible would be the case. The rules for negation are therefore similar to the rules for implication. To prove/introduce `¬¨P`, assume `P` and derive a contradiction `false` (i.e. construct a proof of proposition `false`).  An example of this is the proof of irrationality of root 2.
To eliminate `¬¨P`, given a proof of `P` and a proof of `¬¨ P` we get `false`. 
-/

-- We are trying to prove that "if P then if not P then false"
example : 
  P ‚Üí ¬¨P ‚Üí false := 
begin 
  intro hp, -- we want to prove the implication P ‚Üí (¬¨P ‚Üí false), therefore we use implication introduction
  intro hnp, -- we want to prove the implication (¬¨P) ‚Üí false, therefore we use implication introduction 
  apply hnp,          -- we have a proof of ¬¨P, so we use the elimination for negation to construct a proof of false.
  assumption,
end  






example : P ‚Üí ¬¨P ‚Üí false := 
begin 
intro hp, 
intro hnp, 
exact hnp hp, -- the direct form of implication elimination
end  





lemma not_imply : 
  (P ‚àß ¬¨ Q) ‚Üí ¬¨ (P ‚Üí Q) := 
begin
  intro hpnq, -- we are trying to prove the implication (P ‚àß ¬¨ Q) ‚Üí (¬¨ (P ‚Üí Q))
  -- we want to prove  the negation ¬¨ (P ‚Üí Q), so we use the intro rule for negation 
  intro hpq, 
  cases hpnq with hp hnq,-- we eliminate the conjunction 
  apply hnq, -- we use the elim rule for `¬¨ Q` to change the goal from false to Q
  apply hpq, --we use the elim rule for `P ‚Üí Q` after which we just need to prove `P`.  
  assumption, 
end   



lemma proof_by_contrapositive 
  (P Q : Prop) : (P ‚Üí Q) ‚Üí (¬¨Q ‚Üí ¬¨P) := 
begin 
  intro hpq, 
  intro hnq,
  intro hp,
  apply hnq, 
  apply hpq,
  assumption,
end 






-- Tactic __suffices__:
lemma proof_by_contrapositive_alt
(P Q : Prop) : (P ‚Üí Q) ‚Üí (¬¨Q ‚Üí ¬¨P) := 
begin
  intro hpq, 
  intro hnq, 
  intro hp, 
  suffices hq : Q, from hnq hq, -- this is very much like apply; it changes the goal to the assumption of the implication. It says I only need to prove Q becasue once i do that i can use `hnq` 
  apply hpq, 
  exact hp,
end 

-- Another example of `suffices`
example : 
  P ‚Üí (Q ‚àß R) ‚Üí P ‚àß Q := 
begin
  intros h‚ÇÅ h‚ÇÇ , 
  suffices h‚ÇÉ : Q, from ‚ü®h‚ÇÅ,h‚ÇÉ‚ü©,
  exact h‚ÇÇ.1,
end 




-- Tactic __exfalso__:
example : 
  P ‚àß ¬¨ P ‚Üí Q :=
begin
  intro hpnp, -- we want to prove the implication (P ‚àß ¬¨ P) ‚Üí Q, so we use the intro rule of implication
  cases hpnp with hp hnp, 
  exfalso, -- is a tactic for the backward elimination of `false`. This means from a proof of `false` everything followes. 
  exact hnp hp,
end





example : 
  P ‚àß ¬¨ P ‚Üí Q :=
begin
  intro hpnp, -- we want to prove the implication (P ‚àß ¬¨ P) ‚Üí Q, so we use the intro rule of implication
  cases hpnp with hp hnp, 
  have f : false, from hnp hp, 
  exfalso, -- is a tactic for the backward elimination of `false`. This means from a proof of `false` everything followes. 
  assumption,
end



example : 
  P ‚àß ¬¨ P ‚Üí Q := 
begin
  intro hpnp,
  exfalso, 
  sorry, 
end 


/- 
A __contradiction__ is a collection of propositions which together lead an absuridty, i.e. a proof of `false`. For instance if we have a proof of a proposition `P` and a proof of `¬¨ P` then we can prove `false`. Hence `¬¨ P` contradicts `P`. 

Tactic __contradiction__: The `contradiction` tactic searches for a contradiction among the hypotheses of the current goal. 
-/

example : 
  P ‚àß ¬¨ P ‚Üí Q :=
begin
  intro h, 
  cases h, 
  contradiction,
end








/-! ### Disjunction (or) 

- Tactic for disjunction introduction:  We use the tactic __left__ or __right__ in order to prove a propositional formula of the form `P ‚à® Q`. 


      P
   -------- ‚à®-intro-left 
    P ‚à® Q
 


      Q
   -------- ‚à®-intro-right
     P ‚à® Q


 
- Tactic for disjunction elimination: We use the tactic __cases__ in order to use a proof of `P ‚à® Q` to prove some other proposition. 



  P ‚à® Q     P        Q
            .        .
            .        .
            .        . 
            R        R
----------------------------
            R

-/



-- introduction example 
example (hp : P) : 
  P ‚à® Q ‚à® ¬¨ P :=
begin
  left,
  assumption,
end 





-- introduction example 
example (hq : Q) : 
  P ‚à® Q ‚à® ¬¨ P :=
begin
  right, 
  left, 
  assumption,
end




-- introduction example 
example (hq : Q) : 
  P ‚à® Q ‚à® ¬¨ P :=
begin
  itauto,
end




-- Challenge: fill in the `sorry` below. 
-- introduction example
example (h : P ‚àß Q) : 
  P ‚à® Q :=
begin
  cases h with hp hq, -- we want to prove P ‚à® Q. For this we want to use the elim rule for conjunction. 
  -- we want to prove P ‚à® Q. For this, we use the left/right intro rule for disjunction.
  left, -- if we know P is true then by the intro rule of disjunction we are done. 
  exact hp,
end 





-- elimination example 
lemma or_swap (h :  P ‚à® Q) :
  Q ‚à® P :=
begin
  cases h with hp hq, -- we want to prove P ‚à® Q. For this we use the elim rule of disjunction. 
  {
    right, -- we want to prove Q ‚à® P. For this we use the right intro rule of disjunction
    assumption,
  },
  {
    left, -- we want to prove Q ‚à® P. For this we use the left intro rule of disjunction
    assumption,
  },
end 





/-
The tactic `cases` can be used like `cases h with hp hq` to give customary name to the branches of a disjunction, using `hp` for the first branch and `hq` for the second. 
-/

example : P ‚à® Q ‚Üí Q ‚à® P :=
begin
  intro h, -- we want to prove an implication, so we use the intro rule of ‚Üí 
  apply or_swap, 
  assumption, 
end



example : P ‚à® Q ‚Üí Q ‚à® P :=
begin
  intro h, -- we want to prove an implication, so we use the intro rule of ‚Üí 
  exact or_swap P Q h,
end





theorem conjunction_distrib_disjunction (h : P ‚àß (Q ‚à® R) ) : 
  (P ‚àß Q) ‚à® (P ‚àß R) :=
begin
   cases h with hp hqr, -- we want to eliminate ‚àß in P ‚àß (Q ‚à® R)
   cases hqr, -- we want to eliminate ‚à® in Q ‚à® R
   {
    left, -- intro rule for ‚à® 
    split, -- intro rule for ‚àß 
    -- assumption',
    {
      assumption,
    },
    {
      assumption,
    },
   },
  {
   sorry, 
  },
end








theorem resolve_left : 
  P ‚à® Q ‚Üí ¬¨ Q ‚Üí P := 
begin
  intro hpq, 
  intro hnq, -- the goal is an implication
  cases hpq with hp hq, 
  {
    assumption,
  },
  {
    have f : false, from hnq hq, 
    exfalso, 
    assumption,
  },
end 


theorem resolve_left_alt : 
  P ‚à® Q ‚Üí ¬¨ Q ‚Üí P := 
begin
  intro hpq, 
  intro hnq, -- the goal is an implication
  cases hpq with hp hq, 
  {
    assumption,
  },
  {
    exfalso, 
    apply hnq, 
    exact hq,
  },
end 



theorem resolve_left_alt_alt : 
  P ‚à® Q ‚Üí ¬¨ Q ‚Üí P := 
begin
  intro hpq, 
  intro hnq, -- the goal is an implication
  cases hpq with hp hq, 
  {
    assumption,
  },
  {
    contradiction, -- use it with care (this tactic is non-constructive!)
  },
end 


theorem resolve_right : 
  P ‚à® Q ‚Üí ¬¨ P ‚Üí Q := 
begin
  sorry,  
end 



lemma mul_eq_zero_of_eq_zero_or_eq_zero (m n : ‚Ñï) (h : m = 0 ‚à® n = 0) : 
  m * n = 0 := 
begin
  sorry, 
end   



#check eq_zero_or_eq_zero_of_mul_eq_zero


example (x : ‚Ñ§) (h : x = 1 ‚à® x = -1) : 
  x^2 = 1 := 
begin
  cases h with h‚ÇÅ h‚ÇÇ, 
  {
    rw h‚ÇÅ,
    ring,
  },
  {
    rw h‚ÇÇ, 
    ring,
  },
end 


example {x : ‚Ñ§} (h : x^2 = 1) : x = 1 ‚à® x = -1 :=
begin
  exact sq_eq_one_iff.mp h,
end






section 
variable x : ‚Ñù 
#check le_or_gt
-- the following lemmas are immediate from the definition of `abs`.
#check (abs_of_nonneg :  0 ‚â§ x ‚Üí |x| = x)
#check (abs_of_neg : x < 0 ‚Üí |x| = -x)
end 



lemma lt_abs (x y : ‚Ñù) : 
  x < abs y ‚Üí x < y ‚à® x < -y :=
begin
-- Here's our proof strategy: 
-- Either `y` is non-negative or it is negative. 
--- If `y` is non-negative then `abs y = y` and therefore, our assumption `x < abs y` simplifies to `x < y`. 
---- But if `x < y` then `x < y ‚à® x < -y`
--- If `y` is negative then `abs y = - y` and therefore, our assumption `x < abs y` simplifies to `x < - y`. 
---- But if `x < - y` then `x < y ‚à® x < -y`
  cases le_or_gt 0 y, -- since the order ‚â§  ‚Ñù, ‚â§) is a linear order 
  { 
    rw abs_of_nonneg h,
    intro h‚ÇÅ, 
    left, 
    exact h‚ÇÅ, 
  },
  { 
    rw abs_of_neg h,
    intro h‚ÇÇ, 
    right, 
    exact h‚ÇÇ,
  },
end



lemma abs_lt  (x y : ‚Ñù ) : 
  abs x < y ‚Üî - y < x ‚àß x < y :=
begin
  sorry, 
end









end PROOFS